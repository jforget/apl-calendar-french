fr: Calendrier Républicain
en: French Revolutionary Calendar
calendar-french.apl-1: apl
---
- section: calfr∆license
  calendar-french.apl-1: 1
- fr: |
    La partie texte de ce dépôt git est distribuée sous la licence
    Creative Commons avec attribution et partage dans les mêmes
    conditions (CC-BY-SA). La partie code de ce dépôt est distribuée
    sous la licence GPL version 1.0 ou ultérieure.
- en: |
    The text part of this repository is licensed under the terms of
    Creative Commons, with attribution and share-alike (CC-BY-SA).
    The code part of this repository is licensed with the GPL version
    1.0 or later.
- fr: |
    Ainsi que le requiert la licence GPL, tout fichier de code
    doit commencer par un commentaire décrivant de façon sommaire
    le logiciel et résumant la GPL. La description sommaire en français&nbsp;:

    «&nbsp;Les fonctions de ce script permettent de faire des conversions
    du calendrier grégorien vers le calendrier républicain et vice-versa.&nbsp;»

    Quant au résumé de la GPL, le voici, en anglais (je ne suis
    pas assez calé pour traduire en français un texte de teneur juridique).
- en: |
    As required by the GPL, each file with code must start with a one-line
    description of the program and the summary of the GPL. Here it is.
- code: |
    ∇ calfr∆license
    'APL program to convert Gregorian dates'
    'to French Revolutionary dates and the other way'
    ''
    'Copyright (C) 2015, 2016 Jean Forget  (JFORGET at cpan dot org)'
    ''
    'Build date:'
    ''
    ' This program is distributed under the GNU Public License version 1 or later'
    ''
    ' You can find the text of the license in the LICENSE file or at'
    ' http://www.gnu.org/licenses/gpl-1.0.html.'
    ''
    ' Here is the summary of GPL:'
    ''
    ' This program is free software; you can redistribute it and/or modify'
    ' it under the terms of the GNU General Public License as published by'
    ' the Free Software Foundation; either version 1, or (at your option)'
    ' any later version.'
    ''
    ' This program is distributed in the hope that it will be useful,'
    ' but WITHOUT ANY WARRANTY; without even the implied warranty of'
    ' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the'
    ' GNU General Public License for more details.'
    ''
    ' You should have received a copy of the GNU General Public License'
    ' along with this program; if not, write to the Free Software Foundation,'
    ' Inc., <http://www.fsf.org/>.'
    ∇
- fr: |
    Comme vous pouvez le deviner, ce résumé fait partie intégrante du
    logiciel. Donc, une fois que vous êtes entrés sous APL et que vous
    avez initialisé votre espace de travail avec mon script, vous pouvez
    à tout moment afficher ce résumé.
- en: |
    As you can see, this summary is executable code included in the software.
    So, after you have logged into APL and initialised your workspace with
    my script, you can display this summary at any moment.
- section: 0
  fr: Conversion entre calendriers
  en: Conversion between calendars
- fr: |
    Cette série de fonctions s'applique à trois calendriers
    différents.

    <dl>
    <dt> Le calendrier républicain</dt>
    <dd> Le calendrier républicain a été en vigueur en France
    de 1792 à 1805, mais les fonctions de ce script permettent
    d'utiliser des dates jusqu'à aujourd'hui et au-delà.
    Les dates sont représentées par un vecteur de trois nombres au format AAAA MM JJ.
    Il y a douze mois de 30 jours chacun, plus 5 ou 6&nbsp;jours
    complémentaires à la fin de l'année. Pour des raisons de simplicité,
    ces jours complémentaires constituent un mois supplémentaire numéroté 13.
    </dd>

    <dt> Le calendrier grégorien proleptique</dt>
    <dd> Il s'agit du
    calendrier grégorien, extrapolé dans le passé au-delà
    de l'année 1582.
    Non seulement la règle des années bissextiles est celle du
    calendrier grégorien, mais en plus on considère que l'année
    commence toujours le premier janvier, ce qui n'était pas toujours
    le cas avec les calendriers réellement en usage au Moyen-Âge.
    Les dates grégoriennes sont exprimées sous la forme d'un
    vecteur de trois nombres, au format AAAA MM JJ.</dd>

    <dt> Le <i lang='la'>Rata Die</i> (RD)</dt>
    <dd> C'est un calendrier qui 
    ignore totalement les années.  Il ne compte que les jours, à partir
    du 1<sup>er</sup> janvier de l'an 1 du calendrier grégorien
    proleptique. Très pratique pour les 
    programmes, beaucoup moins pour les humains.
    En fait, le calendrier <i lang='la'>Rata Die</i> ne vous servira
    pas en tant que tel, c'est simplement un intermédiaire entre
    les deux autres. Vous pouvez en avoir besoin quand même par
    exemple pour calculer le nombre de jours entre deux dates.
    </dd>
    </dl>
- en: |
    This group of functions applies to three different calendars.

    <dl>
    <dt>The French Revolutionary calendar</dt>
    <dd> This calendar was in effect
    only in France and only from 1792 to 1805, but the programs allow you to
    use dates past this limit, until today and beyond. The dates are
    entered as vectors of 3 integer numbers YYYY MM DD. This calendar contains
    12 months of 30 days each, plus 5 or 6 additional days. For convenience,
    these additional days are grouped together in what is considered
    as an additional month, numbered 13.
    </dd>

    <dt> The proleptic Gregorian calendar</dt>
    <dd> This is the Gregorian
    calendar, but extending in the past farther than 1582. Not only the leap
    year rule is the same as the Gregorian rule, but also the beginning
    of the year is always on January 1st, although it was not always
    the case during the Middle Ages with the actual calendar. The Gregorian dates
    are implemented as vectors of 3 numbers, in YYYY MM DD order.
    </dd>

    <dt>The <i lang='la'>Rata Die</i> calendar</dt>
    <dd> This calendar ignores
    completely the notion of year. It counts only the days since 
    January 1st, year 1, in the proleptic Gregorian calendar.
    Very convenient for computers, much less for humans.
    Actually, the script just uses the <i lang='la'>Rata Die</i> calendar 
    as a step between the two other calendars.
    You can still find some use with it, for example to compute how many
    days lie between two given dates.
    </dd>
    </dl>

- section: 0
  fr: Mode d'emploi
  en: Instructions for Use

- section: 0
  'fr,en': Installation
  level: 3
- fr: |
    Le fichier <tt>calfr.apl</tt> doit se trouver
    dans le sous-répertoire <tt>workspaces</tt>, pour compatibilité
    avec la commande <tt>)LOAD</tt>. Vous pouvez stocker ce fichier
    dans un sous-répertoire <tt>wslib</tt><var>n</var> (<var>n</var>
    valant de 1 à 9), auquel cas vous devrez utiliser la variante
    <tt>)LOAD</tt>&nbsp;<var>n</var>&nbsp;<tt>calfr.apl</tt>.
    De même, là où le mode d'emploi indique la commande <tt>)LOAD</tt>,
    vous pouvez utiliser les variantes <tt>)XLOAD</tt> ou <tt>)COPY</tt>.
- en: |
    The <tt>calfr.apl</tt> file must be stored in the
    <tt>workspaces</tt> sub-directory of the current directory, 
    so you can use the <tt>)LOAD</tt> APL command. You may store
    it in a <tt>wslib</tt><var>n</var> sub-directory (<var>n</var>
    from 1 to 9) and in this case, you will use the
    <tt>)LOAD</tt>&nbsp;<var>n</var>&nbsp;<tt>calfr.apl</tt>
    variant. Likewise, when the present text shows the command <tt>)LOAD</tt>,
    you can use the <tt>)XLOAD</tt> or <tt>)COPY</tt> variants instead.
- fr: |
    Installation pour GNU-APL à partir de  <a href='https://github.com/jforget/apl-calendar-french'>GitHub</a>&nbsp;:
    vous pouvez cloner le dépôt et copier <tt>workspaces/calfr.apl</tt>
    vers le sous-répertoire <tt>workspaces</tt> de votre répertoire APL.
    Ou alors, affichez <a href='https://github.com/jforget/apl-calendar-french/blob/master/workspaces/calfr.apl'>https://github.com/.../calfr.apl</a>
    en mode <i lang='en'>raw</i> et sauvegardez-le en mode «&nbsp;texte seul&nbsp;»
    dans le sous-répertoire <tt>workspaces</tt> de votre répertoire APL.
- en: |
    Installation for GNU-APL from  <a href='https://github.com/jforget/apl-calendar-french'>GitHub</a>:
    you may clone the repository and copy <tt>workspaces/calfr.apl</tt>
    to the <tt>workspaces</tt> subdirectory of your APL directory.
    Or else, display <a href='https://github.com/jforget/apl-calendar-french/blob/master/workspaces/calfr.apl'>https://github.com/.../calfr.apl</a>
    click on the "raw" button and save the file as "text only"
    into the <tt>workspaces</tt> of your APL directory.
- fr: |
    Installation pour GNU-APL à partir de <a href='https://www.gnu.org/software/apl/Bits_and_Pieces/'>GNU-APL Bits and Pieces</a>&nbsp;:
    sauvegardez le fichier affiché en mode «&nbsp;texte seul&nbsp;» dans le sous-répertoire <tt>workspace</tt>.
    Ne vous préoccupez pas de tout le texte en anglais et en français, il sera masqué lors du chargement sous APL.
- en: |
    Installation for GNU-APL from <a href='https://www.gnu.org/software/apl/Bits_and_Pieces/'>GNU-APL Bits and Pieces</a>:
    save the file as "text only" into the <tt>workspace</tt> subdirectory. Do not bother about all this English and French text,
    il will be hidden while loading the program into APL.
- fr: |
    Installation pour NARS2000 à partir de  <a href='https://github.com/jforget/apl-calendar-french'>GitHub</a>&nbsp;:
    clonez le dépôt et copier <tt>workspaces/calfrnars.atf</tt>
    vers le répertoire <tt>c:\user\moi-meme\Application Data\NARS2000\workspaces</tt> ou, si vous utilisez GNU/Linux et Wine&nbsp;:
    <tt>/home/moi-meme/.wine/drive_c/users/moi-meme/Application Data/NARS2000/workspaces</tt>.
- en: |
    Installation for NARS2000 from  <a href='https://github.com/jforget/apl-calendar-french'>GitHub</a>:
    Clone the repository and copy <tt>workspaces/calfrnars.atf</tt>
    to the <tt>c:\user\myself\Application Data\NARS2000\workspaces</tt> directory or, if you use GNU/Linux and Wine:
    <tt>/home/myself/.wine/drive_c/users/myself/Application Data/NARS2000/workspaces</tt>.
    
- section: 0
  fr: Chargement
  en: Loading
  level: 3
- fr: |
    Chargement dans GNU-APL&nbsp;: vous chargez un espace de travail initialisé avec les fonctions
    de conversion par&nbsp:
- en: |
    Loading into GNU-APL: you initialise an pristine workspace with the conversion functions with:
- code: |
    apl -f workspaces/calfr.apl
- fr: |
    ou bien avec&nbsp;:
- en: |
    or with:
- code: |
    apl
        )LOAD calfr
- fr: |
    Chargement dans NARS2000&nbsp;: vous chargez un espace de travail initialisé avec les fonctions
    de conversion par&nbsp:
- en: |
    Loading into NARS2000: you initialise an pristine workspace with the conversion functions with:
- code: |
        )IN calfrnars
- section: 0
  fr: Utilisation
  en: Basic use
  level: 3
- fr: |
    Vous vous retrouvez alors dans la boucle REPL (lecture,
    évaluation, impression et boucle) d'APL. Vous pouvez alors
    demander la conversion du 13 Vendémiaire IV ainsi&nbsp;:
- en: |
    You are now in the REPL loop of APL. You can
    for example ask to convert 13 Vendémiaire IV in this way:
- code: |
    .
         calfr∆fr2gr 4 1 13
    1795 10 5
- fr: |
    ce qui veut dire que le 13 Vendémiaire IV correspond au 
    5 octobre 1795. Dans l'autre sens, la conversion du 
    27 juillet 1794 se fait ainsi&nbsp;:
- en: |
    which means that 13 Vendémiaire IV is equivalent to
    5th October 1795. In the other direction, converting
    27th July 1794 is:
- code: |
    .
         calfr∆gr2fr 1794 7 27
    2 11 9
- fr: |
    ce qui signifie 9 Thermidor II. Pour avoir la date
    sous forme plus agréable, utilisez <tt>calfr∆prtfr</tt>&nbsp;:
- en: |
    which means 9 Thermidor II. To display the date in
    a more convenient way, use  <tt>calfr∆prtfr</tt> in addition:
- code: |
    .
         calfr∆prtfr 2 11 9
    Nonidi 9 Thermidor II, jour de la Mûre
         calfr∆prtfr calfr∆gr2fr 1794 7 27
    Nonidi 9 Thermidor II, jour de la Mûre
- fr: |
    Et pour avoir la date du jour
- en: |
    And to get the current day
- code: |
    .
         calfr∆prtfr calfr∆gr2fr 3↑⎕TS
- fr: |
    Si vous voulez calculer le nombre de jours entre deux
    dates, utilisez le <i lang='la'>Rata Die</i>. Par
    exemple, vous voulez savoir combien de jours séparent
    le coup d'état du neveu (2 décembre 1851) du coup d'état
    de l'oncle (18 Brumaire VIII). Il suffit de taper&nbsp;:
- en: |
    If you want to compute the number of days between two
    dates, use the <i lang='la'>Rata Die</i> calendar. For example,
    if you want to know how many days elapsed from the 
    uncle's coup (18 Brumaire VIII) and the nephew's coup
    (2nd December 1851), just type:
- code: |
    .
         (calfr∆gr2rd 1851 12 2) - calfr∆fr2rd 8 2 18
    19015
- section: calfr∆usage
  calendar-french.apl-1: 1.5
  level: 3
- fr: |
    Voici l'aide en ligne en anglais.
- en: |
    And here is the embedded help, callable from APL.
- code: |
    ⍝ Installing from GitHub (https://github.com/jforget/apl-calendar-french):
    ⍝     clone the Git repo and copy "workspaces/calfr.apl" to the "workspaces" sub-directory of your GNU-APL directory
    ⍝                         or copy "workspaces/calfrnars.atf" to some place where NARS2000 will find it
    ⍝ Installing from browsing https://www.gnu.org/software/apl/Bits_and_Pieces/
    ⍝     save the webpage as simple text into the "workspaces" sub-directory of your GNU-APL directory
    ∇ calfr∆usage
    'Loading the module from the command line (for GNU-APL):'
    '      apl -f workspaces/calfr.apl'
    'Loading the module from inside GNU-APL:'
    '      )LOAD calfr'
    'Loading the module from inside NARS2000:'
    '      )IN calfrnars'
    'Dates are vectors of 3 numbers YYYY MM DD. e.g.'
    '      DR ←    8 2 18 ⍝ for 18 Brumaire VIII'
    '      DG ← 1794 7 27 ⍝ for 27th July 1794'
    'Gregorian to French Revolutionary:'
    '      DR ← calfr∆gr2fr DG'
    'French Revolutionary to Gregorian:'
    '      DG ← calfr∆fr2gr DR'
    'Pretty-printing:'
    '      STRING ← calfr∆prtfr DR'
    'Pretty-printing today''s date:'
    '      STRING ← calfr∆prtfr calfr∆gr2fr 3↑⎕TS'
    ∇
- section: 0
  level: 3
  fr: Ordres supérieurs
  en: Higher Orders
- fr: |
    Une date est représentée par un vecteur de 3 nombres. On peut
    organiser les dates en un vecteur de dates, qui sera en réalité
    un tableau de nombres. Ainsi, un vecteur de 5 dates sera un tableau de dimension <tt>5 3</tt>.

    Par exemple, le vecteur de dates&nbsp;:
- en: |
    A date is implemented as a vector of 3 numbers. Several dates can
    be gathered into a date-vector, which will actually be an array
    of numbers. Thus, a 5-date vector will be an array with a <tt>5 3</tt> dimension.

    For example, the date vector:
- 
  'en,fr': |
    <table border='1' cellpadding='2'><tr>
    <td>21-JAN-1794</td><td>5-OCT-1795</td><td>9-NOV-1799</td><td>2-DEC-1851</td><td>31-MAR-2015</td>
    </tr></table>
- fr: |
    sera représenté par&nbsp;:
- en: |
    will be implemented by:
- code: |
    .
         DATEVEC 
    1794  1 21
    1795 10  5
    1799 11  9
    1851 12  2
    2015  3 31
         ⍴ DATEVEC 
    5 3
- fr: |
    De la même manière, un tableau <tt>4 5</tt> de dates sera représenté
    par un bloc <tt>4 5 3</tt> de nombres, un bloc <tt>2 2 6</tt> de dates
    sera représenté par un bloc <tt>2 2 6 3</tt> de nombres, et ainsi
    de suite. Les fonctions <tt>calfr∆gr2fr</tt> et <tt>calfr∆fr2gr</tt> sont compatibles
    avec toutes ces structures, sauf lorsque l'on arrive au voisinage du rang maximal.
    Par exemple, sur cette implémentation d'APL le rang maximal est 8, donc les fonctions commencent
    à provoquer des erreurs avec une structure de rang 7.

    Ainsi, la conversion du vecteur ci-dessus se fait par&nbsp;:
- en: |
    Likewise, a <tt>4 5</tt> array of dates will be entered as a <tt>4 5 3</tt>
    block of numbers, and a <tt>2 2 6</tt> block of dates will be entered
    as a <tt>2 2 6 3</tt> block of numbers and so on. The <tt>calfr∆gr2fr</tt>
    and <tt>calfr∆fr2gr</tt> functions can deal with these structures, except when
    using a rank nearly equal to the maximum allowed value. For example,
    in this implementation APL is limited to rank 8, so the <tt>calfr∆gr2fr</tt> and <tt>calfr∆fr2gr</tt>
    functions are limited to rank 6.

    Continuing with the example above, converting the date vector is done with:
- code: |
    .
         calfr∆gr2fr DATEVEC 
      2  5  2
      4  1 13
      8  2 18
     60  3 11
    223  7 11
- fr: |
    Par contre, la fonction <tt>calfr∆prtfr</tt> n'est pas compatible avec les vecteurs
    de dates ou les tableaux de dates. Elle accepte seulement les dates scalaires.
- en: |
    On the other hand, the <tt>calfr∆prtfr</tt> function is not compatible with date vectors
    or date arrays. It works only with scalar dates.
- section: 0
  level: 3
  fr: Utilisation de "enclose"
  en: Enclosed Values
- fr: |
    Je sais que APL permet d'inclure des objets dans d'autres, avec
    les opérateurs <tt>⊂</tt> et <tt>⊃</tt>. Mais cela n'existait pas lorsque j'ai appris APL
    il y a plus de trente ans. J'ai donc écrit le présent script sans chercher à utiliser
    ces opérateurs. J'avais l'intention d'écrire dans un deuxième temps un nouveau script
    qui reposerait sur <tt>⊂</tt> et <tt>⊃</tt>.

    Mais j'ai fait quelques expériences avec le script dans l'état actuel.
    En utilisant la fonction <tt>¨</tt> (<i lang='en'>each</i>), les fonctions
    deviennent compatibles avec les objets «&nbsp;enclosed&nbsp;». Exemple&nbsp;:
- en: |
    I know that APL allow to embed objects into others, using the
    <tt>⊂</tt> and <tt>⊃</tt> operators. But when I learnt APL more than thirty
    years ago, these operators did not exist. So, I have written the present script
    as a first version which does not use these operations. I had the intention of
    writing a second version which would rely on <tt>⊂</tt> et <tt>⊃</tt>.

    But I tried some experiments with the script <i>as is</i>. By using the <tt>¨</tt>
    (each) operator, the functions become compatible with enclosed objects. For example:
- code: |
    .
         V ← 2 2 ⍴ (1792 9 22) (1795 10 5) (1799 11 9), ⊂ 3↑⎕TS
         V
    1792 9 22  1795 10 5  
    1799 11 9  2015 12 24 
         ⍴V
    2 2
         V1 ← calfr∆prtfr ¨ calfr∆gr2fr ¨ V
         V1
    Primidi 1 Vendémiaire I, jour du Raisin        Tridi 13 Vendémiaire IV, jour du Potiron 
    Octidi 18 Brumaire VIII, jour de la Dentelaire Quartidi 4 Nivôse CCXXIV, jour du Soufre 
         ⍴V1
    2 2
- section: 0
  fr: Implémentation
  en: Implementation
- section: 0
  fr: Éléments de date
  en: Date Elements
  level: 3
- fr: |
    Pour extraire l'année d'une date <tt>DATE</tt>, il suffit d'écrire <tt>DATE[1]</tt>.
    Pour en extraire le jour, il suffit d'écrire <tt>DATE[3]</tt>.
    Mais cela ne fonctionne pas avec les vecteurs de dates pour lesquels il faut
    écrire <tt>DATEVEC[;1]</tt> et <tt>DATEVEC[;3]</tt>,
    ni avec les tableaux de dates pour lesquel il faut écrire
    <tt>DATEARRAY[;;1]</tt> et <tt>DATEARRAY[;;3]</tt>.
    Il y a plusieurs façons de s'en tirer et d'avoir une fonction compatible avec
    tous les rangs et toutes les dimensions. La plus simple consiste à écrire&nbsp;:
- en: |
    To extract the year from a date <tt>DATE</tt>, you juste have to write <tt>DATE[1]</tt>.
    To extract the day, just write <tt>DATE[3]</tt>.
    But this does not work with date vectors, for which you need to write
    <tt>DATEVEC[;1]</tt> and <tt>DATEVEC[;3]</tt>,
    which in turn does not work with date arrays, for which you write
    <tt>DATEARRAY[;;1]</tt> and <tt>DATEARRAY[;;3]</tt>.
    There are several ways to use the same functions for all. The simplest way is as follows:
- section: calfr∆year
  calendar-french.apl-1: 2
  level: 4
- code: |
    ∇ R ← calfr∆year D
    R ← D +.× 1 0 0
    ∇
- section: calfr∆month
  calendar-french.apl-1: 3
  level: 4
- code: |
    ∇ R ← calfr∆month D
    R ← D +.× 0 1 0
    ∇
- section: calfr∆day
  calendar-french.apl-1: 4
  level: 4
- code: |
    ∇ R ← calfr∆day D
    R ← D +.× 0 0 1
    ∇
- section: calfr∆zerojanvnd
  fr: Notion de «&nbsp;jour zéro&nbsp;»
  en: Concept of "zeroth day"
  calendar-french.apl-1: 10
  level: 3
- fr: |
    Pour comprendre comment les algorithmes de conversion fonctionnent, il est
    utile d'utiliser la notion de «&nbsp;jour zéro&nbsp;» pour un mois donné.
    Le jour zéro est un jour virtuel situé avant le premier jour du mois et
    après le dernier jour du mois précédent. Ainsi, le 0 mars 2015 se situe
    après le 28 février 2015 et avant le 1<sup>er</sup> mars 2015.
    Le fait que son <i lang='la'>Rata Die</i> soit identique à celui du 28 février
    ne change rien au fait qu'il est censé se situer après le 28 février.
- en: |
    To help the description of the conversion algorithms, it is convenient to use
    the "zeroth day" of a given month. The zeroth day is a virtual day, before
    the first day of the month and after the last day of the previous month.
    For example, 0th March 2015 occurs before 1st March 2015 and after 28th February 2015.
    The fact that 0th March 2015 and 28th February 2015 share the same <i lang='la'>Rata Die</i>
    is irrelevant, 0th March is <em>after</em> 28th February.
- fr: |
    Voici une fonction qui permet de construire le 0 janvier d'une année ou le 0 Vendémiaire
    d'une année&nbsp;:
- en: |
    Here is a function which builds a 0th January or a 0th Vendémiaire for a given year:
- code: |
    ∇ R ← calfr∆zerojanvnd Y
    R ← Y ∘.× 1 0 0
    R ← R + (⍴R) ⍴ 0 1 0
    ∇
- section: calfr∆fr2rd
  fr: Conversion du calendrier républicain vers <i lang='la'>Rata Die</i>
  en: Converting from the French Revolutionary calendar to <i lang='la'>Rata Die</i>
  calendar-french.apl-1: 11
  level: 3
- fr: |
    Commençons par calculer le <i lang='la'>Rata Die</i> d'une date du calendrier républicain.
    Il faut ajouter les termes suivants&nbsp;:

    <ul>
    <li>654414, qui est le <i lang='la'>Rata Die</i> du zéro Vendémiaire I,</li>
    <li>(AAAA - 1) × 365, le nombre de jours du zéro Vendémiaire I au zéro Vendémiaire de l'année demandée,
    en oubliant les jours bissextiles, c'est-à-dire les «&nbsp;6 jour complémentaire&nbsp;»,</li>
    <li>calfr∆frbis(AAAA), qui compte combien d'années bissextiles ont précédé l'année demandée,</li>
    <li>(MM - 1) × 30, le nombre de jours du zéro Vendémiaire au jour zéro du mois demandé,</li>
    <li>JJ, pour arriver finalement au jour demandé.</li>
    </ul>
- en: |
    Let us start by computing the <i lang='la'>Rata Die</i> for a given date in the French Revolutionary calendar.
    This number is the result of the addition of:
    <ul>
    <li>654414, which is the <i lang='la'>Rata Die</i> for zeroth Vendémiaire I,</li>
    <li>(YYYY - 1) × 365, the number of days from zeroth Vendémiaire I to zeroth Vendémiaire of the target year,
    leaving aside the leap days, that is, the «&nbsp;6 jour complémentaire&nbsp;»,</li>
    <li>calfr∆frbis(YYYY), which gives the number of leap years before the target year,</li>
    <li>(MM - 1) × 30, the number of days from zeroth Vendémiaire until the zeroth day of the target month</li>
    <li>DD, to reach the target day.</li>
    </ul>
- fr: |
    En fait, on rassemble toutes les constantes additives en une seule, ce qui donne le calcul suivant&nbsp;:

    <ul>
    <li>AAAA × 365,</li>
    <li>calfr∆frbis(AAAA),</li>
    <li>MM × 30,</li>
    <li>JJ,</li>
    <li>constante additive.</li>
    </ul>
- en: |
    Actually, we reorder the terms to have a single additive constant, which gives the following terms:

    <ul>
    <li>YYYY × 365,</li>
    <li>calfr∆frbis(YYYY),</li>
    <li>MM × 30,</li>
    <li>DD,</li>
    <li>additive constant.</li>
    </ul>
- code: |
    ∇ R ← calfr∆fr2rd DATE
    R ← 654019 + (@calfr∆frbis@ @calfr∆year@ DATE) + DATE +.× 365 30 1
    ∇
- section: calfr∆frbis
  fr: Nombre d'années bissextiles avant l'année N (calendrier républicain)
  en: Number of leap years before year N (French Revolutionary calendar)
  level: 3
  calendar-french.apl-1: 12
- fr: |
    Cette fonction donne le nombre d'années bissextiles
    qui ont eu lieu avant l'année en paramètre. Elle permet
    d'affiner le calcul de la date, par rapport à l'estimation
    obtenue en prenant des années à 365&nbsp;jours.

    La règle pour les années bissextiles était une règle astronomique
    jusqu'en l'an XX, le premier Vendémiaire devant coïncider avec
    l'équinoxe d'automne. Ensuite, la réforme de Gilbert Romme
    aurait fait appliquer une règle arithmétique analogue à 
    celle du calendrier grégorien.

    La règle de Romme indique qu'une année est bissextile si elle est divisible par 4, sauf
    si elle est divisible par 100, mais elle l'est quand même si
    elle est divisible par 400, sauf si elle est divisible par 4000.
- en: |
    This subroutine gives the number of leap years preceding
    the current year. It allows refining
    the date computation, initialised with a computation using only
    365-day years.

    Until year XX, the leap year rule is an astronomical rule,
    stating that the 1st Vendémiaire should happen on the 
    autumn equinox. Then from year XX on, there would have
    been an arithmetical rule, similar to the Gregorian Calendar
    rule, introduced by Gilbert Romme's reform.

    The Romme rule states that a year is a leap year if it is a multiple
    of 4, except if it is a multiple of 100, but is nevertheless leap if
    it is a multiple of 400, except if it is a multiple of 4000.
- fr: |
    Voici la comparaison des deux règles pour le début de l'ère
    républicaine. Les deux colonnes «&nbsp;arithmétique&nbsp;»
    simulent l'hypothèse où la règle arithmétique aurait été
    en vigueur dès le début. Les deux colonnnes «&nbsp;astronomique&nbsp;»
    présentent le cas réel&nbsp;: règle astronomique au début
    et règle arithmétique ensuite. Les colonnes «&nbsp;Bis&nbsp;?&nbsp;»
    indiquent par un «&nbsp;X&nbsp;» si l'année est bissextile
    en fonction de la règle utilisée. Les colonnes «&nbsp;frbis&nbsp;»
    indiquent le nombre d'années bissextiles avant l'année considérée,
    ce qui correspond au résultat de la fonction <tt>calfr∆frbis</tt>.
- en: |
    See below the comparison between both rules for the beginning
    of the Republican Era. The two "Arithmetic" columns simulate
    the hypothesis of applying the arithmetic rule from the beginning
    of the era. The "Astronomical" columns show the actual case, 
    with the astronomical rule at first and the arithmetic rule
    after. The columns "Leap?" indicates leap years with an "X".
    The columns "frbis" show the number of leap years before the current
    year, which should be the result of the <tt>calfr∆frbis</tt> function.
- fr: |
    <table border='1'>
    <tr align='center'><td>     </td><td colspan='2'>Arithmétique</td><td colspan='2'>Astronomique</td></tr>
    <tr align='center'><td>Année</td><td>Bis ?</td><td>frbis </td><td>Bis ?</td><td>frbis </td></tr>
- en: |
    <table border='1'>
    <tr align='center'><td>     </td><td colspan='2'> Arithmetic </td><td colspan='2'>Astronomical</td></tr>
    <tr align='center'><td>Year </td><td>Leap?</td><td>frbis </td><td>Leap?</td><td>frbis </td></tr>

- 
  'en,fr': |
    <tr align='center'><td>  1  </td><td>&nbsp;</td><td>  0   </td><td>&nbsp;</td><td>  0   </td></tr>
    <tr align='center'><td>  2  </td><td>&nbsp;</td><td>  0   </td><td>&nbsp;</td><td>  0   </td></tr>
    <tr align='center'><td>  3  </td><td>&nbsp;</td><td>  0   </td><td>  x   </td><td>  0   </td></tr>
    <tr align='center'><td>  4  </td><td>  x   </td><td>  0   </td><td>&nbsp;</td><td>  1   </td></tr>
    <tr align='center'><td>  5  </td><td>&nbsp;</td><td>  1   </td><td>&nbsp;</td><td>  1   </td></tr>
    <tr align='center'><td>  6  </td><td>&nbsp;</td><td>  1   </td><td>&nbsp;</td><td>  1   </td></tr>
    <tr align='center'><td>  7  </td><td>&nbsp;</td><td>  1   </td><td>  x   </td><td>  1   </td></tr>
    <tr align='center'><td>  8  </td><td>  x   </td><td>  1   </td><td>&nbsp;</td><td>  2   </td></tr>
    <tr align='center'><td>  9  </td><td>&nbsp;</td><td>  2   </td><td>&nbsp;</td><td>  2   </td></tr>
    <tr align='center'><td> 10  </td><td>&nbsp;</td><td>  2   </td><td>&nbsp;</td><td>  2   </td></tr>
    <tr align='center'><td> 11  </td><td>&nbsp;</td><td>  2   </td><td>  x   </td><td>  2   </td></tr>
    <tr align='center'><td> 12  </td><td>  x   </td><td>  2   </td><td>&nbsp;</td><td>  3   </td></tr>
    <tr align='center'><td> 13  </td><td>&nbsp;</td><td>  3   </td><td>&nbsp;</td><td>  3   </td></tr>
    <tr align='center'><td> 14  </td><td>&nbsp;</td><td>  3   </td><td>&nbsp;</td><td>  3   </td></tr>
    <tr align='center'><td> 15  </td><td>&nbsp;</td><td>  3   </td><td>  x   </td><td>  3   </td></tr>
    <tr align='center'><td> 16  </td><td>  x   </td><td>  3   </td><td>&nbsp;</td><td>  4   </td></tr>
    <tr align='center'><td> 17  </td><td>&nbsp;</td><td>  4   </td><td>&nbsp;</td><td>  4   </td></tr>
    <tr align='center'><td> 18  </td><td>&nbsp;</td><td>  4   </td><td>&nbsp;</td><td>  4   </td></tr>
    <tr align='center'><td> 19  </td><td>&nbsp;</td><td>  4   </td><td>&nbsp;</td><td>  4   </td></tr>
    <tr align='center'><td> 20  </td><td>  x   </td><td>  4   </td><td>  x   </td><td>  4   </td></tr>
    <tr align='center'><td> 21  </td><td>&nbsp;</td><td>  5   </td><td>&nbsp;</td><td>  5   </td></tr>
    <tr align='center'><td> 22  </td><td>&nbsp;</td><td>  5   </td><td>&nbsp;</td><td>  5   </td></tr>
    <tr align='center'><td> 23  </td><td>&nbsp;</td><td>  5   </td><td>&nbsp;</td><td>  5   </td></tr>
    <tr align='center'><td> 24  </td><td>  x   </td><td>  5   </td><td>  x   </td><td>  5   </td></tr>
    <tr align='center'><td> 25  </td><td>&nbsp;</td><td>  6   </td><td>&nbsp;</td><td>  6   </td></tr>
    </table>

- fr: |
    En fait, les deux règles n'engendrent une différence que pour
    les années IV, VIII, XII et XVI. Donc le sous-programme effectue
    le calcul avec la règle arithmétique dès l'an I, puis rectifie le résultat
    si l'on a l'une de ces quatre années. N'oubliez pas que le sous-programme
    commence par soustraire 1 à l'année, c'est pour cela qu'il teste
    les valeurs 3, 7, 11 et 15.
- en: |
    Actually, the two rules differ only for years IV, VIII, XII
    and XVI. So the subroutine applies the arithmetical rule
    starting from year I
    and then checks the year, tweaking the result is the year
    is one of the four exceptions. Remember that the subroutine begins
    with subtracting 1 from the year, therefore the values to 
    check are 3, 7, 11 and 15.
- code: |
    ∇ R ← calfr∆frbis YEAR
    YEAR ← YEAR - 1
    R ← - / ⌊ YEAR ∘.÷ 4 100 400 4000
    R ← R + YEAR ∈ 3 7 11 15
    ∇
- section: calfr∆gr2rd
  fr: Conversion du calendrier grégorien vers <i lang='la'>Rata Die</i>
  en: Converting from the Gregorian calendar to <i lang='la'>Rata Die</i>
  calendar-french.apl-1: 13
  level: 3
- fr: |
    Malgré les critères pour les années bissextiles, la conversion du calendrier
    républicain vers le <i lang='la'>Rata Die</i> est simple, comparée avec
    la fonction correspondante du calendrier grégorien. En effet, la période
    bouche-trou (jours complémentaires) est à la fin de l'année donc sa longueur variable
    et plus faible que les mois normaux n'a pas d'influence sur les calculs.
    En revanche, dans le calendrier grégorien, le mois bouche-trou, février, apparaît
    au début de l'année et complique le calcul pour les mois suivants.
    De plus, la longueur des autres mois est variable&nbsp;: 30 ou 31 jours.
- en: |
    Although the leap year criteria for the French Revolutionary calendar are a bit
    complicated, converting from this calendar to <i lang='la'>Rata Die</i>
    is simple when compared with the Gregorian Calendar. The filler period (additional days) is at the end of the year,
    so its shorter and variable duration has no impact on the computation.
    On the other hand, in the Gregorian calendar, the filler period is February,
    near the beginning of the year, so its influence spans over the main part of
    the year. In addition, the duration of the other months is variable: 30 or 31 days.
- fr: |
    C'est pourquoi nous introduisons un nouveau calendrier, le «&nbsp;calendrier grégorien
    décalé&nbsp;». Dans ce calendrier, les mois de janvier et février sont transférés
    à la fin de l'année précédente et l'année commence au 1<sup>er</sup> mars.
    De plus, pour les raisons exposées ci-dessous, les mois sont numérotés de 4 (mars)
    à 15 (février), pour la raison exposée ci-dessous.

    Ainsi le 28 février 2015 devient <tt>2014&nbsp;15&nbsp;28</tt> et le premier mars 2015
    devient <tt>2015&nbsp;4&nbsp;1</tt>.
- en: |
    This is why we use yet another calendar, the "shifted Gregorian calendar".
    In this calendar, the January and February months are assigned to the previous year
    so the year will begin on the First of March. In addition, for reasons shown
    below, the months are numbered from 4 (March) to 15 (February).

    So, 28th February 2015 becomes <tt>2014&nbsp;15&nbsp;28</tt> and 1st March 2015
    becomes <tt>2015&nbsp;4&nbsp;1</tt>.
- fr: |
    Dans le tableau ci-dessous, la partie gauche s'intéresse au nombre de mois à 31&nbsp;jours
    qui précèdent le mois courant dans l'année décalée. La partie droite explique le calcul du nombre de jours
    depuis le zéro mars jusqu'au jour zéro du mois courant.
- en: |
    In the table below, the left side show how to count the number of 31-day months
    before the current month and since the beginning of the shifted year.
    And the right side shows how to compute the number of days from 0th March to the
    zeroth day of the current month.
-
  'fr,en': |
    <table border='1'>
    <tr align='center'><th>M</th><th>nb31</th><th> M</th><th>0.6 × M</th><th>¯2 + ⌊ 0.6 × M</th><th>M</th><th>  ln   </th><th> nb</th><th>30.6 × M</th><th>¯122 + ⌊ 30.6 × M</th></tr>
    <tr align='right'> <td>M</td><td>  0 </td><td> 4</td><td>  2.4  </td><td>      0       </td><td>M</td><td>  31   </td><td>  0</td><td>   122.4</td><td>        0        </td></tr>
    <tr align='right'> <td>A</td><td>  1 </td><td> 5</td><td>  3.0  </td><td>      1       </td><td>A</td><td>  30   </td><td> 31</td><td>   153.0</td><td>       31        </td></tr>
    <tr align='right'> <td>M</td><td>  1 </td><td> 6</td><td>  3.6  </td><td>      1       </td><td>M</td><td>  31   </td><td> 61</td><td>   183.6</td><td>       61        </td></tr>
    <tr align='right'> <td>J</td><td>  2 </td><td> 7</td><td>  4.2  </td><td>      2       </td><td>J</td><td>  30   </td><td> 92</td><td>   214.2</td><td>       92        </td></tr>
    <tr align='right'> <td>J</td><td>  2 </td><td> 8</td><td>  4.8  </td><td>      2       </td><td>J</td><td>  31   </td><td>122</td><td>   244.8</td><td>      122        </td></tr>
    <tr align='right'> <td>A</td><td>  3 </td><td> 9</td><td>  5.4  </td><td>      3       </td><td>A</td><td>  31   </td><td>153</td><td>   275.4</td><td>      153        </td></tr>
    <tr align='right'> <td>S</td><td>  4 </td><td>10</td><td>  6.0  </td><td>      4       </td><td>S</td><td>  30   </td><td>184</td><td>   306.0</td><td>      184        </td></tr>
    <tr align='right'> <td>O</td><td>  4 </td><td>11</td><td>  6.6  </td><td>      4       </td><td>O</td><td>  31   </td><td>214</td><td>   336.6</td><td>      214        </td></tr>
    <tr align='right'> <td>N</td><td>  5 </td><td>12</td><td>  7.2  </td><td>      5       </td><td>N</td><td>  30   </td><td>245</td><td>   367.2</td><td>      245        </td></tr>
    <tr align='right'> <td>D</td><td>  5 </td><td>13</td><td>  7.8  </td><td>      5       </td><td>D</td><td>  31   </td><td>275</td><td>   397.8</td><td>      275        </td></tr>
    <tr align='right'> <td>J</td><td>  6 </td><td>14</td><td>  8.4  </td><td>      6       </td><td>J</td><td>  31   </td><td>306</td><td>   428.4</td><td>      306        </td></tr>
    <tr align='right'> <td>F</td><td>  7 </td><td>15</td><td>  9.0  </td><td>      7       </td><td>F</td><td>28? 29?</td><td>337</td><td>   459.0</td><td>      337        </td></tr>
    </table>
- fr: |
    Voici donc la fonction&nbsp;:
- en: |
    So here is the function:
- code: |
    ∇ R ← calfr∆gr2rd DATE; DIM
    DIM ← ⍴ DATE
    DATE←(DIM ⍴0 1 0) + DATE + (2≥@calfr∆month@ DATE) ∘.× ¯1 12 0
    R ← ¯428 + (-/ ⌊ (@calfr∆year@ DATE) ∘.÷ 4 100 400) + ⌊DATE +.× 365 30.6 1
    ∇
- section: calfr∆rd2fr
  fr: Conversion du <i lang='la'>Rata Die</i> vers le calendrier républicain 
  en: Converting from <i lang='la'>Rata Die</i> to the French Revolutionary calendar
  calendar-french.apl-1: 14
  level: 3
- fr: |
    Principe&nbsp;: en partant de la valeur <i lang='la'>Rata Die</i>, la fonction évalue
    une fourchette pour l'année, qui peut donc s'étaler de <tt>YL</tt> à <tt>YH</tt>.
    Supposons que l'on parte de la valeur <i lang='la'>Rata Die</i> 672800,
    avec une fourchette de 50 à 53.
    Le sous-programme construit ensuite la liste <tt>YR</tt> des années de la fourchette, soit dans
    l'exemple un vecteur <tt>YR</tt> valant <tt>50&nbsp;51&nbsp;52&nbsp;53</tt>.
    Puis le sous-programme transforme ce vecteur d'années en vecteurs de dates <tt>DR</tt>,
    correspondant au zéro Vendémiaire de chaque année, ce qui donnerait&nbsp;:
- en: |
    Here is how it is done. Starting from the <i lang='la'>Rata Die</i> value, the function
    computes a <tt>YL</tt>-to-<tt>YH</tt> interval of candidate years.
    Suppose we start with <i lang='la'>Rata Die</i> value 672800 and that the
    candidate years are 50 to 53 inclusive.
    The program computes the full list of candidate years <tt>YR</tt>, that is,
    in this example, the vector <tt>50&nbsp;51&nbsp;52&nbsp;53</tt>.
    Then the program turns this year vector into a date vector <tt>DR</tt>, consisting of
    the zeroth Vendémiaire for each year.
-
  'fr,en': |
    <pre>
        DR
    50 1 0
    51 1 0
    52 1 0
    53 1 0  
    </pre>
- fr: |
    La fonction <a href='#calfr∆fr2rd'><tt>calfr∆fr2rd</tt></a> convertit cela en <i lang='la'>Rata Die</i>
- en: |
    The <a href='#calfr∆fr2rd'><tt>calfr∆fr2rd</tt></a> function computes the <i lang='la'>Rata Die</i> of these dates
-
  'fr,en': |
    <pre>
        NR
    672311 672676 673041 673407
    </pre>
- fr: |
    et compare avec la valeur initiale 672800, ce qui donne&nbsp;:
- en: |
    and compares with the initial value 672800, which gives:
-
  'fr,en': |
    <pre>
        CM
    1 1 0 0
    </pre>
- fr: |
    Cela permet d'avoir l'année de la date
    en prenant l'année la plus élevée pour laquelle la valeur dans <tt>NR</tt> est inférieure
    à la valeur du paramètre d'entrée <tt>N</tt>.
    Dans l'exemple ci-dessus, on en déduit que l'année est 51. En faisant la différence
    des deux valeurs <i lang='la'>Rata Die</i>, on obtient 124, ce qui est facilement
    converti en 4 Pluviôse (<tt>5 4</tt>) avec de simples divisions par 30.
- en: |
    The proper year is the highest year for which
    the zeroth Vendémiaire has a <i lang='la'>Rata Die</i> value less than the value
    given as a parameter. In the example above, this would give year 51. By subtracting
    the <i lang='la'>Rata Die</i> of the zeroth Vendémiaire from the parameter, we get
    124, which can easily yield "4 Pluviôse" (<tt>5 4</tt>) with simple divisions by 30.
- fr: |
    Remarque&nbsp;: dans l'exemple ci-dessus, la plage d'années avait 4&nbsp;valeurs.
    Dans la réalité, la plage n'aura qu'une seule valeur, parfois deux au voisinage
    des changements d'année. Le choix de 4&nbsp;valeurs avait un rôle pédagogique.
    En fait, la première date pour laquelle la plage comporte trois valeurs est l'année
    3744 et il faut attendre 7480 pour qu'elle compte quatre valeurs.
- en: |
    Remark: in the example above, the interval of candidate years has 4 values. Actually,
    in most cases there will be only one candidate year when the date is far from the
    year's beginning and end, and there will be two around
    the years' ends. Using 4 values instead of just 1 or 2 has a pedagogical purpose.
    Actually, the first date with a 3-value range occurs in year 3744 and the first
    4-value range appears in year 7480.
- fr: |
    Autre remarque&nbsp;: lorsque le paramètre en entrée n'est pas un scalaire, mais un
    vecteur, un tableau ou une structure d'ordre plus élevé, les variables intermédiaires
    ont également un ordre plus élevé. Le seul point délicat est de savoir ce qui
    se passe lorsque l'une des valeurs <i lang='la'>Rata Die</i> donne une plage
    de 1&nbsp;valeur et l'autre une plage de 2&nbsp;valeurs (par exemple). Dans ce cas,
    la plage de valeurs du premier <i lang='la'>Rata Die</i> est allongée à 2&nbsp;valeurs
    pour conserver la rectangularité de la variable <tt>YR</tt>.
- en: |
    Other remark: when the input parameter is not a scalar, but a vector, array or higher
    ranked structure, the various variables also have a higher rank. The only question
    is what happens if a first <i lang='la'>Rata Die</i> value yields a 1-value 
    range for the candidate years and another one yields a 2-value range. In this case,
    the shorter range is extended to 2 values, so the <tt>YR</tt> variable will be
    properly rectangular.
- code: |
    ∇ R ← calfr∆rd2fr N; ⎕IO; N1; YH; YL; YI; YR; DR; NR; CM; Y; M; D
    ⎕IO ← 1
    N1 ← N - 654414
    YH ← ⌈ N1 ÷ 365.24
    YL ← ⌈ N1 ÷ 365.34
    YI ← 0 , ⍳ ⌈/,YH-YL
    YR ← YL ∘.+ YI
    DR ← @calfr∆zerojanvnd@ YR
    NR ← @calfr∆fr2rd@ DR
    CM ← NR < N ∘.+ (⍴YI)⍴0
    Y ← ⌈/YR×CM
    D ← N - ⌈/NR×CM
    M ← ⌈D÷30
    D ← D - 30 × M - 1
    R ← (Y ∘.× 1 0 0) + (M ∘.× 0 1 0) + D ∘.× 0 0 1
    ∇
- fr: |
    Résumé des variables locales&nbsp;:

    <table>
    <tr><td>YH</td><td>year (high)      </td><td>borne supérieure de la plage d'années                 </td></tr> 
    <tr><td>YL</td><td>year (low)       </td><td>borne inférieure de la plage d'années                 </td></tr> 
    <tr><td>YI</td><td>year increments  </td><td>vecteur d'incréments pour construire la plage d'années</td></tr> 
    <tr><td>YR</td><td>year range       </td><td>plage d'années                                        </td></tr> 
    <tr><td>DR</td><td>date range       </td><td>plage de dates (0 Vendémiaire)                        </td></tr> 
    <tr><td>NR</td><td>number range     </td><td>plage de nombres (<i lang='la'>Rata Die</i>)          </td></tr> 
    <tr><td>CM</td><td>comparison       </td><td>résultat de la comparaison                            </td></tr> 
    <tr><td>Y </td><td>year final value </td><td>valeur finale de l'année                              </td></tr> 
    <tr><td>D </td><td>day final value  </td><td>valeur finale du jour                                 </td></tr> 
    <tr><td>M </td><td>month final value</td><td>valeur finale du mois                                 </td></tr> 
    </table>
- en: |
    Summary of the local variables:

    <table>
    <tr><td>YH</td><td>year (high)                              </td></tr>
    <tr><td>YL</td><td>year (low)                               </td></tr>
    <tr><td>YI</td><td>year increments, to build the year range </td></tr>
    <tr><td>YR</td><td>year range                               </td></tr>
    <tr><td>DR</td><td>date range (0 Vendémiaire)               </td></tr>
    <tr><td>NR</td><td>number range (<i lang='la'>Rata Die</i>) </td></tr>
    <tr><td>CM</td><td>comparison                               </td></tr>
    <tr><td>Y </td><td>year final value                         </td></tr>
    <tr><td>D </td><td>day final value                          </td></tr>
    <tr><td>M </td><td>month final value                        </td></tr>
    </table>

- fr: |
    Voici un exemple d'exécution avec un vecteur en entrée. Notons que pour le premier
    nombre, la plage de valeurs de la date comportait une seule année, l'année 400. 
    Mais par «&nbsp;contamination&nbsp;» de la dernière valeur, cette plage a été
    étendue à trois années.
- en: |
    Here is an example using a vector, not a scalar, as the input parameter.
    Note that for the first value, the year range should include one year only,
    but because of the fourth value, this range was extended to three years.
-
  'fr,en': |
    <pre>
          V←800505 800511 800879 2115019
          calfr∆rd2fr V
     400 12 30
     400 13  6
     402  1  3
    4000  1  1
          YH
    400 401 402 4000
          YL
    400 400 401 3998
          YI
    0 1 2
          YR
     400  401  402
     400  401  402
     401  402  403
    3998 3999 4000
          DR
     400 1 0
     401 1 0
     402 1 0

     400 1 0
     401 1 0
     402 1 0

     401 1 0
     402 1 0
     403 1 0

    3998 1 0
    3999 1 0
    4000 1 0
          NR
     800145  800511  800876
     800145  800511  800876
     800511  800876  801241
    2114288 2114653 2115018
          CM
    1 0 0
    1 0 0
    1 1 0
    1 1 1
          Y
    400 400 402 4000
          M
    12 13 1 1
          D
    30 6 3 1
    </pre>
- section: calfr∆rd2gr
  fr: Conversion du <i lang='la'>Rata Die</i> vers le calendrier grégorien 
  en: Converting from <i lang='la'>Rata Die</i> to the Gregorian calendar
  calendar-french.apl-1: 15
  level: 3
- fr: |
    Pour cette fonction, il y a deux possibilités. La première consiste à
    convertir le <i lang='la'>Rata Die</i> en grégorien décalé, puis le
    grégorien décalé en grégorien. Ou alors, convertir directement en
    grégorien en utilisant deux fois le mécanisme de plage
    de valeurs possibles utilisé dans <a href='#calfr∆rd2fr'>calfr∆rd2fr</a>, une première
    fois pour calculer l'année, la seconde fois pour calculer le mois.
    C'est cette deuxième possibilité que j'ai choisie.

    Le calcul de la borne supérieure et de la borne inférieure de la plage de
    mois nécessite une division par 29,5 et une division par 31. Du coup,
    l'estimation du mois est beaucoup moins précise que l'estimation
    de l'année. La plage aura donc trois valeurs dès que le nombre de jours
    vaut 620. Or, on sait que le nombre de jours ne dépassera pas 366 et
    que le numéro du mois ne dépassera pas 12. Dans ces conditions,
    la plage aura au maximum deux valeurs. Donc on ne s'embête pas, on prend
    toujours une plage à deux valeurs pour calculer le mois, même si l'estimation
    avait donné une plage à une seule valeur.
- en: |
    For this function, there are two options. The first consists in converting
    <i lang='la'>Rata Die</i> to shifted Gregorian and then converting from 
    shifted Gregorian to standard Gregorian. The second possibility consists
    in using twice the "possible value range", the first time to compute the 
    year (as in <a href='#calfr∆rd2fr'><tt>calfr∆rd2fr</tt></a>) and the second time
    to compute the month. I use this second way.

    Computing the upper and lower limits of the candidate month range involves
    a division by 29.5 and a division by 31. So, the 3-value range will arrive
    sooner than with the candidate years. It will arrive with a day number 620.
    Even if the guesstimate of the month is coarser than the estimated value of
    the year, we know that the computation will never involve more than 366 days or 12 months,
    so the range will have at most two values. Therefore, we do not bother refining the
    estimation and we always use a two-value range for the month, even if the
    guesstimate would give a single-value range.
- code: |
    ∇ R ← calfr∆rd2gr N; ⎕IO; YH; YL; YI; YR; DR; NR; CM; Y; M; D; ML; MR
    ⎕IO ← 1
    YH ← ⌈ N ÷ 365.24
    YL ← ⌈ N ÷ 365.25
    YI ← 0 , ⍳ ⌈/,YH-YL
    YR ← YL ∘.+ YI
    DR ← @calfr∆zerojanvnd@ YR
    NR ← @calfr∆gr2rd@ DR
    CM ← NR < N ∘.+ (⍴YI)⍴0
    Y ← ⌈/YR×CM
    D ← N - ⌈/NR×CM
    ML ← ⌈D÷31
    MR ← ML ∘.+ 0 1
    DR ← (Y ∘.× 2 3 ⍴ 1 0 0) + (MR ∘.× 0 1 0)
    NR ← @calfr∆gr2rd@ DR
    CM ← NR < N ∘.+ 0 0
    M ← ⌈/MR×CM
    D ← N - ⌈/NR×CM
    R ← (Y ∘.× 1 0 0) + (M ∘.× 0 1 0) + D ∘.× 0 0 1
    ∇
- section: 0
  fr: Les fonctions de conversion
  en: The Conversion Functions
- section: calfr∆gr2fr
  level: 3
  fr: Conversion du calendrier grégorien vers le calendrier républicain
  en: Conversion from the Gregorian calendar to the French Revolutionary calendar
  calendar-french.apl-1: 16
- fr: |
    Après tous ces calculs compliqués, la fonction de conversion est simplissime,
    un peu décevante.
- en: |
    After all theses complicated computations, the final conversion function is
    a bit of an anti-climax.
- code: |
    ∇ R ← calfr∆gr2fr D
    R ← @calfr∆rd2fr@ @calfr∆gr2rd@ D
    ∇
- section: calfr∆fr2gr
  level: 3
  fr: Conversion du calendrier républicain vers le calendrier grégorien
  en: Conversion from the French Revolutionary calendar to the Gregorian calendar
  calendar-french.apl-1: 17
- fr: |
    Idem pour l'autre fonction de conversion.
- en: |
    Same thing for the other conversion function.
- code: |
    ∇ R ← calfr∆fr2gr D
    R ← @calfr∆rd2gr@ @calfr∆fr2rd@ D
    ∇
- section: 0
  fr: Affichage des dates
  en: Displaying the Dates
- section: calfr∆prtfr
  level: 3
  fr: Affichage d'une date du calendrier républicain
  en: Displaying a French Revolutionary calendar date
  calendar-french.apl-1: 20
- fr: |
    Rassembler les éléments de la date&nbsp;: nom du jour, numéro, nom du mois, etc,
    ne pose pas de problème. Ce qui en pose, c'est le fait que le résultat comporte
    des espaces superflus et qu'il faut les éliminer tout en conservant les espaces
    nécessaires.
- en: |
    Gathering the various date elements: day name, day number, month name, etc,
    is simple enough. What is more difficult is that this gives a result with 
    extraneous spaces and that we have to get rid of these spaces, while keeping
    the required spaces.
- code: |
    ∇ R ← calfr∆prtfr D; DAY; MONTH; CM; ⎕IO
    ⎕IO ← 1
    DAY ← 10 8 ⍴ 'Décadi  Primidi Duodi   Tridi   QuartidiQuintidiSextidi Septidi Octidi  Nonidi  '
    R ← DAY[1 + 10 | @calfr∆day@ D;]
    MONTH ← 13 11 ⍴ 'VendémiaireBrumaire   Frimaire   Nivôse     Pluviôse   Ventôse    Germinal   Floréal    Prairial   Messidor   Thermidor  Fructidor  jour compl.'
    R ← R, ' ', (⍕ @calfr∆day@ D), ' ', (MONTH[@calfr∆month@ D;]), ' ', (@calfr∆roman@ @calfr∆year@ D), ', jour ', @calfr∆feasts@[¯30 + D +.× 0 30 1;]
    CM ← ' ' ≠ R
    R ← (CM ∨ 0,¯1↓CM) / R
    R ← (⌽∨\⌽R≠' ')/R
    ∇
- section: calfr∆roman
  level: 3
  fr: Conversion d'un nombre en chiffres romains
  en: Converting a number into Roman numerals
  calendar-french.apl-1: 21
- fr: |
    Si le nombre dépasse 3999, il conserve son expression en chiffres indo-arabes.
    Idem s'il est négatif ou nul.
- en: |
    If the number is higher than 3999, it is displayed with indo-arab numerals. Same thing if zero or negative.
- code: |
    ∇ R ← calfr∆roman N; NODES
    → ((N>0) ∧ N<4000)/CONV
    R ← ⍕ N
    → 0
    CONV:
    NODES ← 40 4 ⍴ (@calfr∆nodes@ 'IVX'), (@calfr∆nodes@ 'XLC'), (@calfr∆nodes@ 'CDM'), @calfr∆nodes@ 'M??'
    R ← ,NODES[⎕IO + 30 20 10 0 + 10 10 10 10 ⊤ N;]
    R ← (R≠' ')/R
    ∇
- section: calfr∆nodes
  level: 3
  fr: Construction des nœuds de la numération romaine
  en: Building the nodes of Roman numerals
  calendar-french.apl-1: 22
- fr: |
    Le terme «&nbsp;nœuds&nbsp;» provient de <cite>Histoire comparée des numérations écrites</cite>
    par Geneviève Guitel et je l'ai traduit en «&nbsp;nodes&nbsp;». Dans une numération de base <var>b</var>, un nœud est un nombre
    de la forme <var>k × b<sup>n</sup></var>, avec <var>k</var> entre 0 et <var>b</var>&nbsp;-&nbsp;1.
    Cette fonction participe à la construction d'une chaîne de caractères comportant 10
    nœuds de la base 10 écrit en chiffres romains. Chaque nœud est complété par des espaces pour avoir une
    longueur de 4.
- en: |
    The word "nodes" comes from the French-written book <cite lang='fr'>Histoire comparée des numérations écrites</cite>
    (which would translate to "Compared History of Written Number Systems"), by Geneviève Guitel.
    The original french word is "nœuds".
    When dealing with radix <var>b</var>, a node is a number <var>k × b<sup>n</sup></var>, with <var>k</var> between 0 and <var>b</var>&nbsp;-&nbsp;1.
    This function provides a step to build a string which contains 10 substrings, each one being a radix-10 node, 
    written with Roman numerals. Each node is filled with spaces to reach a common length of 4 chars.
- code: |
    ∇ R ← calfr∆nodes CH
    R ← (' ', CH) [ ' IVX' ⍳ '    I   II  III IV  V   VI  VII VIIIIX  ' ]
    ∇
- section: calfr∆feasts
  level: 3
  fr: Fêtes du calendrier républicain
  en: Feasts of the French Revolutionary calendar
  calendar-french.apl-1: 23
- fr: |
    Cette fonction énumère les fêtes des 366 jours du calendier républicain.
- en: |
    This function lists all 366 feasts for all days in a French Revolutionary calendar year.
- code: |
    ∇ R ← calfr∆feasts; V
    ⍝ include here the contents of feasts
    ∇
- fr: |
    Elle renvoie un tableau de 366 lignes de 21 caractères, une ligne par jour.
- en: |
    It returns an array of 366 lines of 21 chars, one line per day.
-
  'fr,en': |
    <pre>
          4 21↑calfr∆feasts
    du Raisin            
    du Safran            
    de la Châtaigne      
    de la Colchique      
    </pre>

- section: 0
  fr: Tests internes
  en: Built-in Tests
- section: calfr∆testdata
  level: 3
  fr: Données pour les tests
  en: Test Data
  calendar-french.apl-1: 30
- fr: |
    Cette fonction renvoie un tableau de <var>n</var> lignes et 7 colonnes,
    contenant les données pour tester les fonctions de conversion.
    Chaque ligne contient la date grégorienne, au format AAAA MM JJ, 
    la date républicaine au format AAAA MM JJ et la valeur <i lang='la'>Rata Die</i>.
    Exemple&nbsp;:
- en: |
    This function gives an array of <var>n</var> lines and 7 columns, holding
    the data to test the conversion functions.
    Each line contains the Gregorian date in YYYY MM DD format, then the
    French Revolutionary date in YYYY MM DD format and lastly the <i lang='la'>Rata Die</i> value.
    For example
-
  'fr,en': |
    <pre>
          2 7↑calfr∆testdata
    1792  9 22 1  1  1 654415
    1793 10 23 2  2  2 654811
    </pre>
- fr: |
    Les premières lignes montrent que le 22 septembre 1792 correspond au 1<sup>er</sup> Vendémiaire I
    avec une valeur <i lang='la'>Rata Die</i> 654415. De même, le 23 octobre 1973 correspond au
    2 Brumaire II, <i lang='la'>Rata Die</i> 654811.

    Les valeurs adoptées sont les valeurs utilisées dans les tests de
    <a href='https://metacpan.org/pod/DateTime::Calendar::FrenchRevolutionary'>DateTime::Calendar::FrenchRevolutionary</a>.
    Le programme qui les formatte avec la syntaxe APL est disponible sur
    <a href='https://github.com/jforget/French-Revolution-calendar-utilities'>GitHub</a>.
    Et un utilitaire (en Perl) est fourni dans le présent projet pour insérer les valeurs dans le script final.
- en: |
    The first lines of the test data show that the 22nd September 1792 converts to 1st Vendémiaire I,
    with a <i lang='la'>Rata Die</i> value of 654415. Likewise, the 23rd October 1793 converts to 
    2 Brumaire II, that is, <i lang='la'>Rata Die</i> 654811.

    The values used in these built-in tests come from the Perl module
    <a href='https://metacpan.org/pod/DateTime::Calendar::FrenchRevolutionary'>DateTime::Calendar::FrenchRevolutionary</a>.
    The program which turns these dates into an APL code chunk is available on
    <a href='https://github.com/jforget/French-Revolution-calendar-utilities'>GitHub</a>.
    And a utility script (in Perl) in this project inserts the contents of <tt>testapl</tt>
    in the final script.
- code: |
    ∇ R ← calfr∆testdata; V; L
    ⍝ include here the contents of testapl
    L ← (⍴ V) ÷ 7
    R ← (L, 7) ⍴ V
    ∇
- section: calfr∆teststring
  fr: Données alphanumériques de tests
  en: Test alphanumeric data
  calendar-french.apl-1: 31
  level: 3

- fr: |
    Cette fonction donne pour chaque ligne de <a href='#calfr∆testdata'><tt>calfr∆testdata</tt></a>
    le résultat attendu de la fonction <a href='#calfr∆prtfr'><tt>calfr∆prtfr</tt></a>.
    Par exemple&nbsp;:
- en: |
    This function gives the expected <a href='#calfr∆prtfr'><tt>calfr∆prtfr</tt></a> result for each
    corresponding line ot <a href='#calfr∆testdata'><tt>calfr∆testdata</tt></a>.
    For example
-
  'fr,en': |
    <pre>
          2 52↑calfr∆teststring
    Primidi 1 Vendémiaire I, jour du Raisin             
    Duodi 2 Brumaire II, jour du Céleri                 
    </pre>
- fr: |
    L'origine des données et l'utilitaire pour les insérer dans le script
    sont les mêmes que pour <a href='#calfr∆testdata'><tt>calfr∆testdata</tt></a>.
- en: |
    The strings are produced and inserted into the final code in the
    same fashion as <a href='#calfr∆testdata'><tt>calfr∆testdata</tt></a>.
- code: |
    ∇ R ← calfr∆teststring; V; L
    V ← ''
    ⍝ include here the contents of testapl1
    ∇
- section: calfr∆alltests
  level: 3
  calendar-french.apl-1: 35
  fr: Lancement de tous les tests
  en: Run all the tests
- code: |
    ∇ calfr∆alltests
    @calfr∆testfr2rd@
    @calfr∆testgr2rd@
    @calfr∆testrd2fr@
    @calfr∆testrd2gr@
    @calfr∆testgr2fr@
    @calfr∆testfr2gr@
    @calfr∆testprtfr@
    ∇
- section: calfr∆testfr2rd
  level: 3
  calendar-french.apl-1: 40
- fr: |
    Cette fonction teste <a href='#calfr∆fr2rd'><tt>calfr∆fr2rd</tt></a> avec une variété de
    paramètres en entrée&nbsp;: deux dates scalaires, un vecteur de dates, un tableau
    de dates, jusqu'à une structure de dates de rang 7 (donc une structure d'entiers
    de rang 8). Les tests sont effectués une première fois avec <tt>⎕IO</tt>
    à 1, une seconde fois avec <tt>⎕IO</tt> à 0.

    Chaque test élémentaire tombant en erreur affiche un message. Si tous les tests
    élémentaires réussissent, la fonction principale de test affiche un seul message.
- en: |
    This function checks <a href='#calfr∆fr2rd'><tt>calfr∆fr2rd</tt></a> with a wide set of parameters:
    two scalar dates, a date vector, a date array, and so on until a rank-7 structure
    of dates (that is, a rank-8 block of integers). Tests are run a first time with <tt>⎕IO</tt>
    set to 1, a second time with <tt>⎕IO</tt> set to 0.

    Each failed subtest prints an error message. If everything succeeds, the main
    test function prints a single message.
- code: |
    ∇ calfr∆testfr2rd; IO; ⎕IO; OK; PARAM; EXPEC; RESUL
    ⎕IO ← 1
    EXPEC ← @calfr∆testdata@[;7]
    PARAM ← @calfr∆testdata@[;4 5 6]
    OK ← 1
    IO ← 2
    LOOP:
    IO ← IO - 1
    ⎕IO ← IO
    OK ←OK ∧ @calfr∆test0fr2rd@ 1
    OK ←OK ∧ @calfr∆test0fr2rd@ 2
    OK ←OK ∧ @calfr∆test1fr2rd@ 1 ↑ ⍴PARAM ⍝ checking with the full vector
    OK ←OK ∧ @calfr∆test1fr2rd@ 5 5
    OK ←OK ∧ @calfr∆test1fr2rd@ 5 3 3
    OK ←OK ∧ @calfr∆test1fr2rd@ 5 2 2 2
    OK ←OK ∧ @calfr∆test1fr2rd@ 7 ⍴ 2 ⍝ checking max allowed rank
    → IO / LOOP
    → (OK=0) / 0 ⍝ exit if the errors are already reported
    'Checking calfr∆fr2rd : no errors'
    ∇
- section: calfr∆test0fr2rd
  level: 4
  calendar-french.apl-1: 41
- fr: |
    Cette fonction teste <a href='#calfr∆fr2rd'><tt>calfr∆fr2rd</tt></a> pour une date scalaire.
    <tt>⎕IO</tt> est initialisé avec la variable globale <tt>IO</tt>, en fait
    une variable locale de la fonction appelante <a href='#calfr∆testfr2rd'><tt>calfr∆testfr2rd</tt></a>
    transmise globalement, comme c'est le cas pour <tt>PARAM</tt> (paramètres à utiliser
    pour les fonctions) et <tt>EXPEC</tt> (résultat attendu ou <i lang='en'>expected result</i>).
- en: |
    This function checks <a href='#calfr∆fr2rd'><tt>calfr∆fr2rd</tt></a> with a scalar date.
    <tt>⎕IO</tt> is initialised with the global variable<tt>IO</tt>, actually
    a variable local to the upper function <a href='#calfr∆testfr2rd'><tt>calfr∆testfr2rd</tt></a>
    and transmitted globally. The same transmission mechanism applies to <tt>PARAM</tt>
    (parameters for the checked function) and <tt>EXPEC</tt> (expected results).
- code: |
    ∇ R  ← calfr∆test0fr2rd N; LIB; PAR; EXP; GOT; NERR
    ⎕IO ← 1
    EXP ← EXPEC[N]
    PAR ← PARAM[N;]
    LIB  ← 'Checking calfr∆fr2rd with scalar date ', (⍕PAR), ' and ⎕IO ', ⍕IO
    ⎕IO ← IO
    GOT  ← @calfr∆fr2rd@ PAR
    → ((⍴⍴EXP)=⍴⍴GOT)/CHKDATA ⍝ no need to check dimension, it is the empty vector
    LIB, ' Wrong rank: expected ', (⍕⍴⍴EXP), ', got ', ⍕⍴⍴GOT
    R ← 0
    → 0
    CHKDATA:
    NERR ← +/,EXP≠GOT
    → (NERR=0) / CHKIO
    LIB, ' Data errors: ', ⍕ NERR
    R ← 0
    → 0
    CHKIO:
    → (IO=⎕IO) / SUCCESS
    LIB, ' ⎕IO clobbered: was ', (⍕IO), ' became ', ⍕⎕IO
    R ← 0
    → 0
    SUCCESS:
    R ← 1
    → 0
    ∇
- section: calfr∆test1fr2rd
  level: 4
  calendar-french.apl-1: 42
- fr: |
    Cette fonction teste <a href='#calfr∆fr2rd'><tt>calfr∆fr2rd</tt></a> pour un vecteur de dates
    ou une structure de rang supérieur.
    <tt>⎕IO</tt> est initialisé avec la variable globale <tt>IO</tt>, en fait
    une variable locale de la fonction appelante <a href='#calfr∆testfr2rd'><tt>calfr∆testfr2rd</tt></a>
    transmise globalement.
- en: |
    This function checks <a href='#calfr∆fr2rd'><tt>calfr∆fr2rd</tt></a> with a date vector
    or a higher rank structure.
    <tt>⎕IO</tt> is initialised with the global variable<tt>IO</tt>, actually
    a variable local to the upper function <a href='#calfr∆testfr2rd'><tt>calfr∆testfr2rd</tt></a>
    and transmitted globally.
- code: |
    ∇ R  ← calfr∆test1fr2rd DIM; LIB; EXP; GOT; NERR
    LIB  ← 'Checking calfr∆fr2rd with dimension ', (⍕DIM), ' and ⎕IO ', ⍕IO
    EXP ← DIM ⍴ EXPEC
    ⎕IO ← IO
    GOT  ← @calfr∆fr2rd@ (DIM, 3) ⍴ PARAM
    → ((⍴⍴EXP)=⍴⍴GOT)/CHKDIM
    LIB, ' Wrong rank: expected ', (⍕⍴⍴EXP), ', got ', ⍕⍴⍴GOT
    R ← 0
    → 0
    CHKDIM:
    → (∧/(⍴EXP)=⍴GOT)/CHKDATA
    LIB, ' Wrong dimension: expected ', (⍕⍴EXP), ', got ', ⍕⍴GOT
    R ← 0
    → 0
    CHKDATA:
    NERR ← +/,EXP≠GOT
    → (NERR=0) / CHKIO
    LIB, ' Data errors: ', ⍕ NERR
    R ← 0
    → 0
    CHKIO:
    → (IO=⎕IO) / SUCCESS
    LIB, ' ⎕IO clobbered: was ', (⍕IO), ' became ', ⍕⎕IO
    R ← 0
    → 0
    SUCCESS:
    R ← 1
    → 0
    ∇

- section: calfr∆testgr2rd
  level: 4
  calendar-french.apl-1: 50
- code: |
    ∇ calfr∆testgr2rd; IO; ⎕IO; OK; PARAM; EXPEC; RESUL
    ⎕IO ← 1
    EXPEC ← @calfr∆testdata@[;7]
    PARAM ← @calfr∆testdata@[;1 2 3]
    OK ← 1
    IO ← 2
    LOOP:
    IO ← IO - 1
    ⎕IO ← IO
    OK ←OK ∧ @calfr∆test0gr2rd@ 1
    OK ←OK ∧ @calfr∆test0gr2rd@ 2
    OK ←OK ∧ @calfr∆test1gr2rd@ 1 ↑ ⍴PARAM ⍝ checking with the full vector
    OK ←OK ∧ @calfr∆test1gr2rd@ 5 5
    OK ←OK ∧ @calfr∆test1gr2rd@ 5 3 3
    OK ←OK ∧ @calfr∆test1gr2rd@ 5 2 2 2
    OK ←OK ∧ @calfr∆test1gr2rd@ 7 ⍴ 2 ⍝ checking max allowed rank
    → IO / LOOP
    → (OK=0) / 0 ⍝ exit if the errors are already reported
    'Checking calfr∆gr2rd : no errors'
    ∇
- section: calfr∆test0gr2rd
  level: 4
  calendar-french.apl-1: 51
- code: |
    ∇ R  ← calfr∆test0gr2rd N; LIB; PAR; EXP; GOT; NERR
    ⎕IO ← 1
    EXP ← EXPEC[N]
    PAR ← PARAM[N;]
    LIB  ← 'Checking calfr∆gr2rd with scalar date ', (⍕PAR), ' and ⎕IO ', ⍕IO
    ⎕IO ← IO
    GOT  ← @calfr∆gr2rd@ PAR
    → ((⍴⍴EXP)=⍴⍴GOT)/CHKDATA ⍝ no need to check dimension, it is the empty vector
    LIB, ' Wrong rank: expected ', (⍕⍴⍴EXP), ', got ', ⍕⍴⍴GOT
    R ← 0
    → 0
    CHKDATA:
    NERR ← +/,EXP≠GOT
    → (NERR=0) / CHKIO
    LIB, ' Data errors: ', ⍕ NERR
    R ← 0
    → 0
    CHKIO:
    → (IO=⎕IO) / SUCCESS
    LIB, ' ⎕IO clobbered: was ', (⍕IO), ' became ', ⍕⎕IO
    R ← 0
    → 0
    SUCCESS:
    R ← 1
    → 0
    ∇
- section: calfr∆test1gr2rd
  level: 4
  calendar-french.apl-1: 52
- code: |
    ∇ R  ← calfr∆test1gr2rd DIM; LIB; EXP; GOT; NERR
    LIB  ← 'Checking calfr∆gr2rd with dimension ', (⍕DIM), ' and ⎕IO ', ⍕IO
    EXP ← DIM ⍴ EXPEC
    GOT ← @calfr∆gr2rd@ (DIM, 3) ⍴ PARAM
    → ((⍴⍴EXP)=⍴⍴GOT)/CHKDIM
    LIB, ' Wrong rank, expected ', (⍕⍴⍴EXP), ', got ', ⍕⍴⍴GOT
    R ← 0
    → 0
    CHKDIM:
    → (∧/(⍴EXP)=⍴GOT)/CHKDATA
    LIB, ' Wrong dimension, expected ', (⍕⍴EXP), ', got ', ⍕⍴GOT
    R ← 0
    → 0
    CHKDATA:
    NERR ← +/,EXP≠GOT
    → (NERR=0) / CHKIO
    LIB, ' Data errors: ', ⍕ NERR
    R ← 0
    → 0
    CHKIO:
    → (IO=⎕IO) / SUCCESS
    LIB, ' ⎕IO clobbered: was ', (⍕IO), ' became ', ⍕⎕IO
    R ← 0
    → 0
    SUCCESS:
    R ← 1
    → 0
    ∇
- section: calfr∆testrd2fr
  level: 4
  calendar-french.apl-1: 60
- code: |
    ∇ calfr∆testrd2fr; IO; ⎕IO; OK; PARAM; EXPEC; RESUL
    ⎕IO ← 1
    EXPEC ← @calfr∆testdata@[;4 5 6]
    PARAM ← @calfr∆testdata@[;7]
    OK ← 1
    IO ← 2
    LOOP:
    IO ← IO - 1
    ⎕IO ← IO
    OK ←OK ∧ @calfr∆test0rd2fr@ 1
    OK ←OK ∧ @calfr∆test0rd2fr@ 2
    OK ←OK ∧ @calfr∆test1rd2fr@ 1 ↑ ⍴PARAM ⍝ checking with the full vector
    OK ←OK ∧ @calfr∆test1rd2fr@ 3 3
    OK ←OK ∧ @calfr∆test1rd2fr@ 15 3
    OK ←OK ∧ @calfr∆test1rd2fr@ 2 3 3
    OK ←OK ∧ @calfr∆test1rd2fr@ 6 ⍴ 2 ⍝ checking max allowed rank
    → IO / LOOP
    → (OK=0) / 0 ⍝ exit if the errors are already reported
    'Checking calfr∆rd2fr : no errors'
    ∇
- section: calfr∆test0rd2fr
  level: 4
  calendar-french.apl-1: 61
- code: |
    ∇ R  ← calfr∆test0rd2fr N; LIB; PAR; EXP; GOT; NERR
    ⎕IO ← 1
    EXP ← EXPEC[N;]
    PAR ← PARAM[N]
    LIB  ← 'Checking calfr∆rd2fr with scalar RD value ', (⍕PAR), ' and ⎕IO ', ⍕IO
    ⎕IO ← IO
    GOT  ← @calfr∆rd2fr@ PAR
    → ((⍴⍴EXP)=⍴⍴GOT)/CHKDIM
    LIB, ' Wrong rank: expected ', (⍕⍴⍴EXP), ', got ', ⍕⍴⍴GOT
    R ← 0
    → 0
    CHKDIM:
    → (∧/(⍴EXP)=⍴GOT)/CHKDATA
    LIB, ' Wrong dimensions: expected ', (⍕⍴EXP), ', got ', ⍕⍴GOT
    R ← 0
    → 0
    CHKDATA:
    NERR ← +/,∨/EXP≠GOT
    → (NERR=0) / CHKIO
    LIB, ' Data errors: ', ⍕ NERR
    R ← 0
    → 0
    CHKIO:
    → (IO=⎕IO) / SUCCESS
    LIB, ' ⎕IO clobbered: was ', (⍕IO), ' became ', ⍕⎕IO
    R ← 0
    → 0
    SUCCESS:
    R ← 1
    → 0
    ∇
- section: calfr∆test1rd2fr
  level: 4
  calendar-french.apl-1: 62
- code: |
    ∇ R ← calfr∆test1rd2fr DIM; EXP; GOT; LIB; NERR
    LIB  ← 'Checking calfr∆rd2fr with dimension ', (⍕DIM), ' and ⎕IO ', ⍕IO
    EXP ← (DIM, 3) ⍴ EXPEC
    GOT ← @calfr∆rd2fr@ DIM ⍴ PARAM
    → ((⍴⍴EXP)=⍴⍴GOT)/CHKDIM
    LIB, ' Wrong rank, expected ', (⍕⍴⍴EXP), ', got ', ⍕⍴⍴GOT
    R ← 0
    → 0
    CHKDIM:
    → (∧/(⍴EXP)=⍴GOT)/CHKDATA
    LIB, ' Wrong dimension, expected ', (⍕⍴EXP), ', got ', ⍕⍴GOT
    R ← 0
    → 0
    CHKDATA:
    NERR ← +/,∨/EXP≠GOT
    → (NERR=0) / CHKIO
    LIB, ' Data errors: ', ⍕ NERR
    R ← 0
    → 0
    CHKIO:
    → (IO=⎕IO) / SUCCESS
    LIB, ' ⎕IO clobbered: was ', (⍕IO), ' became ', ⍕⎕IO
    R ← 0
    → 0
    SUCCESS:
    R ← 1
    → 0
    ∇
- section: calfr∆testrd2gr
  level: 4
  calendar-french.apl-1: 70
- code: |
    ∇ calfr∆testrd2gr; IO; ⎕IO; OK; PARAM; EXPEC; RESUL
    ⎕IO ← 1
    EXPEC ← @calfr∆testdata@[;1 2 3]
    PARAM ← @calfr∆testdata@[;7]
    OK ← 1
    IO ← 2
    LOOP:
    IO ← IO - 1
    ⎕IO ← IO
    OK ←OK ∧ @calfr∆test0rd2gr@ 1
    OK ←OK ∧ @calfr∆test0rd2gr@ 2
    OK ←OK ∧ @calfr∆test1rd2gr@ 1 ↑ ⍴PARAM ⍝ checking with the full vector
    OK ←OK ∧ @calfr∆test1rd2gr@ 3 3
    OK ←OK ∧ @calfr∆test1rd2gr@ 15 3
    OK ←OK ∧ @calfr∆test1rd2gr@ 2 3 3
    → IO / LOOP
    → (OK=0) / 0 ⍝ exit if the errors are already reported
    'Checking calfr∆rd2gr : no errors'
    ∇
- section: calfr∆test0rd2gr
  level: 4
  calendar-french.apl-1: 71
- code: |
    ∇ R  ← calfr∆test0rd2gr N; LIB; PAR; EXP; GOT; NERR
    ⎕IO ← 1
    EXP ← EXPEC[N;]
    PAR ← PARAM[N]
    LIB  ← 'Checking calfr∆rd2gr with scalar RD value ', (⍕PAR), ' and ⎕IO ', ⍕IO
    ⎕IO ← IO
    GOT  ← @calfr∆rd2gr@ PAR
    → ((⍴⍴EXP)=⍴⍴GOT)/CHKDIM
    LIB, ' Wrong rank: expected ', (⍕⍴⍴EXP), ', got ', ⍕⍴⍴GOT
    R ← 0
    → 0
    CHKDIM:
    → (∧/(⍴EXP)=⍴GOT)/CHKDATA
    LIB, ' Wrong dimensions: expected ', (⍕⍴EXP), ', got ', ⍕⍴GOT
    R ← 0
    → 0
    CHKDATA:
    NERR ← +/,∨/EXP≠GOT
    → (NERR=0) / CHKIO
    LIB, ' Data errors: ', ⍕ NERR
    R ← 0
    → 0
    CHKIO:
    → (IO=⎕IO) / SUCCESS
    LIB, ' ⎕IO clobbered: was ', (⍕IO), ' became ', ⍕⎕IO
    R ← 0
    → 0
    SUCCESS:
    R ← 1
    → 0
    ∇
- section: calfr∆test1rd2gr
  level: 4
  calendar-french.apl-1: 72
- code: |
    ∇ R ← calfr∆test1rd2gr DIM; EXP; GOT; LIB; NERR
    LIB  ← 'Checking calfr∆rd2gr with dimension ', (⍕DIM), ' and ⎕IO ', ⍕IO
    EXP ← (DIM, 3) ⍴ EXPEC
    GOT ← @calfr∆rd2gr@ DIM ⍴ PARAM
    → ((⍴⍴EXP)=⍴⍴GOT)/CHKDIM
    LIB, ' Wrong rank, expected ', (⍕⍴⍴EXP), ', got ', ⍕⍴⍴GOT
    R ← 0
    → 0
    CHKDIM:
    → (∧/(⍴EXP)=⍴GOT)/CHKDATA
    LIB, ' Wrong dimension, expected ', (⍕⍴EXP), ', got ', ⍕⍴GOT
    R ← 0
    → 0
    CHKDATA:
    NERR ← +/,∨/EXP≠GOT
    → (NERR=0) / CHKIO
    LIB, ' Data errors: ', ⍕ NERR
    R ← 0
    → 0
    CHKIO:
    → (IO=⎕IO) / SUCCESS
    LIB, ' ⎕IO clobbered: was ', (⍕IO), ' became ', ⍕⎕IO
    R ← 0
    → 0
    SUCCESS:
    R ← 1
    → 0
    ∇

- section: calfr∆testgr2fr
  level: 4
  calendar-french.apl-1: 74
- code: |
    ∇ calfr∆testgr2fr; IO; ⎕IO; OK; PARAM; EXPEC; RESUL
    ⎕IO ← 1
    EXPEC ← @calfr∆testdata@[;4 5 6]
    PARAM ← @calfr∆testdata@[;1 2 3]
    OK ← 1
    IO ← 2
    LOOP:
    IO ← IO - 1
    ⎕IO ← IO
    OK ←OK ∧ @calfr∆test0gr2fr@ 1
    OK ←OK ∧ @calfr∆test0gr2fr@ 2
    OK ←OK ∧ @calfr∆test1gr2fr@ 1 ↑ ⍴PARAM ⍝ checking with the full vector
    OK ←OK ∧ @calfr∆test1gr2fr@ 3 3
    OK ←OK ∧ @calfr∆test1gr2fr@ 15 3
    OK ←OK ∧ @calfr∆test1gr2fr@ 2 3 3
    OK ←OK ∧ @calfr∆test1gr2fr@ 6 ⍴ 2 ⍝ checking max allowed rank
    → IO / LOOP
    → (OK=0) / 0 ⍝ exit if the errors are already reported
    'Checking calfr∆gr2fr : no errors'
    ∇
- section: calfr∆test0gr2fr
  level: 4
  calendar-french.apl-1: 75
- code: |
    ∇ R  ← calfr∆test0gr2fr N; LIB; PAR; EXP; GOT; NERR
    ⎕IO ← 1
    EXP ← EXPEC[N;]
    PAR ← PARAM[N;]
    LIB  ← 'Checking calfr∆gr2fr with scalar RD value ', (⍕PAR), ' and ⎕IO ', ⍕IO
    ⎕IO ← IO
    GOT  ← @calfr∆gr2fr@ PAR
    → ((⍴⍴EXP)=⍴⍴GOT)/CHKDIM
    LIB, ' Wrong rank: expected ', (⍕⍴⍴EXP), ', got ', ⍕⍴⍴GOT
    R ← 0
    → 0
    CHKDIM:
    → (∧/(⍴EXP)=⍴GOT)/CHKDATA
    LIB, ' Wrong dimensions: expected ', (⍕⍴EXP), ', got ', ⍕⍴GOT
    R ← 0
    → 0
    CHKDATA:
    NERR ← +/,∨/EXP≠GOT
    → (NERR=0) / CHKIO
    LIB, ' Data errors: ', ⍕ NERR
    R ← 0
    → 0
    CHKIO:
    → (IO=⎕IO) / SUCCESS
    LIB, ' ⎕IO clobbered: was ', (⍕IO), ' became ', ⍕⎕IO
    R ← 0
    → 0
    SUCCESS:
    R ← 1
    → 0
    ∇
- section: calfr∆test1gr2fr
  level: 4
  calendar-french.apl-1: 76
- code: |
    ∇ R ← calfr∆test1gr2fr DIM; EXP; GOT; LIB; NERR
    LIB  ← 'Checking calfr∆gr2fr with dimension ', (⍕DIM), ' and ⎕IO ', ⍕IO
    EXP ← (DIM, 3) ⍴ EXPEC
    GOT ← @calfr∆gr2fr@ (DIM, 3) ⍴ PARAM
    → ((⍴⍴EXP)=⍴⍴GOT)/CHKDIM
    LIB, ' Wrong rank, expected ', (⍕⍴⍴EXP), ', got ', ⍕⍴⍴GOT
    R ← 0
    → 0
    CHKDIM:
    → (∧/(⍴EXP)=⍴GOT)/CHKDATA
    LIB, ' Wrong dimension, expected ', (⍕⍴EXP), ', got ', ⍕⍴GOT
    R ← 0
    → 0
    CHKDATA:
    NERR ← +/,∨/EXP≠GOT
    → (NERR=0) / CHKIO
    LIB, ' Data errors: ', ⍕ NERR
    R ← 0
    → 0
    CHKIO:
    → (IO=⎕IO) / SUCCESS
    LIB, ' ⎕IO clobbered: was ', (⍕IO), ' became ', ⍕⎕IO
    R ← 0
    → 0
    SUCCESS:
    R ← 1
    → 0
    ∇
- section: calfr∆testfr2gr
  level: 4
  calendar-french.apl-1: 77
- code: |
    ∇ calfr∆testfr2gr; IO; ⎕IO; OK; PARAM; EXPEC; RESUL
    ⎕IO ← 1
    EXPEC ← @calfr∆testdata@[;1 2 3]
    PARAM ← @calfr∆testdata@[;4 5 6]
    OK ← 1
    IO ← 2
    LOOP:
    IO ← IO - 1
    ⎕IO ← IO
    OK ←OK ∧ @calfr∆test0fr2gr@ 1
    OK ←OK ∧ @calfr∆test0fr2gr@ 2
    OK ←OK ∧ @calfr∆test1fr2gr@ 1 ↑ ⍴PARAM ⍝ checking with the full vector
    OK ←OK ∧ @calfr∆test1fr2gr@ 3 3
    OK ←OK ∧ @calfr∆test1fr2gr@ 15 3
    OK ←OK ∧ @calfr∆test1fr2gr@ 2 3 3
    OK ←OK ∧ @calfr∆test1fr2gr@ 6 ⍴ 2 ⍝ checking max allowed rank
    → IO / LOOP
    → (OK=0) / 0 ⍝ exit if the errors are already reported
    'Checking calfr∆fr2gr : no errors'
    ∇
- section: calfr∆test0fr2gr
  level: 4
  calendar-french.apl-1: 78
- code: |
    ∇ R  ← calfr∆test0fr2gr N; LIB; PAR; EXP; GOT; NERR
    ⎕IO ← 1
    EXP ← EXPEC[N;]
    PAR ← PARAM[N;]
    LIB  ← 'Checking calfr∆fr2gr with scalar RD value ', (⍕PAR), ' and ⎕IO ', ⍕IO
    ⎕IO ← IO
    GOT  ← @calfr∆fr2gr@ PAR
    → ((⍴⍴EXP)=⍴⍴GOT)/CHKDIM
    LIB, ' Wrong rank: expected ', (⍕⍴⍴EXP), ', got ', ⍕⍴⍴GOT
    R ← 0
    → 0
    CHKDIM:
    → (∧/(⍴EXP)=⍴GOT)/CHKDATA
    LIB, ' Wrong dimensions: expected ', (⍕⍴EXP), ', got ', ⍕⍴GOT
    R ← 0
    → 0
    CHKDATA:
    NERR ← +/,∨/EXP≠GOT
    → (NERR=0) / CHKIO
    LIB, ' Data errors: ', ⍕ NERR
    R ← 0
    → 0
    CHKIO:
    → (IO=⎕IO) / SUCCESS
    LIB, ' ⎕IO clobbered: was ', (⍕IO), ' became ', ⍕⎕IO
    R ← 0
    → 0
    SUCCESS:
    R ← 1
    → 0
    ∇
- section: calfr∆test1fr2gr
  level: 4
  calendar-french.apl-1: 79
- code: |
    ∇ R ← calfr∆test1fr2gr DIM; EXP; GOT; LIB; NERR
    LIB  ← 'Checking fr2gr with dimension ', (⍕DIM), ' and ⎕IO ', ⍕IO
    EXP ← (DIM, 3) ⍴ EXPEC
    GOT ← @calfr∆fr2gr@ (DIM, 3) ⍴ PARAM
    → ((⍴⍴EXP)=⍴⍴GOT)/CHKDIM
    LIB, ' Wrong rank, expected ', (⍕⍴⍴EXP), ', got ', ⍕⍴⍴GOT
    R ← 0
    → 0
    CHKDIM:
    → (∧/(⍴EXP)=⍴GOT)/CHKDATA
    LIB, ' Wrong dimension, expected ', (⍕⍴EXP), ', got ', ⍕⍴GOT
    R ← 0
    → 0
    CHKDATA:
    NERR ← +/,∨/EXP≠GOT
    → (NERR=0) / CHKIO
    LIB, ' Data errors: ', ⍕ NERR
    R ← 0
    → 0
    CHKIO:
    → (IO=⎕IO) / SUCCESS
    LIB, ' ⎕IO clobbered: was ', (⍕IO), ' became ', ⍕⎕IO
    R ← 0
    → 0
    SUCCESS:
    R ← 1
    → 0
    ∇
- section: calfr∆testprtfr
  level: 4
  calendar-french.apl-1: 80
- code: |
    ∇ calfr∆testprtfr; TD; TS; I; IMAX; PAR; EXP; GOT; N; IO; ⎕IO
    IO ← 2
    LOOPIO:
    IO ← IO - 1
    'Checking calfr∆prtfr with the full vector (a bit slow) and ⎕IO ', ⍕IO
    ⎕IO ← 1
    TD ← @calfr∆testdata@[; 4 5 6 ]
    TS ← @calfr∆teststring@
    I ← 0
    N ← 0
    IMAX ← (⍴ TS)[1]
    LOOP:
    ⎕IO ← 1
    I ← I + 1
    → (I > IMAX)/END
    PAR ← TD[I;]
    EXP ← TS[I;]
    EXP ← (⌽∨\⌽EXP≠' ')/EXP
    ⎕IO ← IO
    GOT ← @calfr∆prtfr@ PAR
    → ((⍴ EXP)  = ⍴ GOT)/NEXT
    'Different length: ', (⍕⍴ EXP), ' ', ⍕⍴GOT
    'Expected: ', EXP
    'Got     : ', GOT
    N ← N + 1
    → LOOP
    NEXT:
    → (∧/EXP=GOT)/LOOP
    'Different content'
    'Expected: ', EXP
    'Got     : ', GOT
    N ← N + 1
    → LOOP
    END:
    'Data errors with ⎕IO = ', (⍕IO), ' : ', ⍕N
    → IO / LOOPIO 
    ∇

- section: 0
  fr: Voir également
  en: See Also
- section: 0
  fr: Logiciels
  en: Software
  level: 3
- fr: |
    En Perl&nbsp;: <a href='http://metacpan.org/module/DateTime::Calendar::FrenchRevolutionary'><span class='tt'>DateTime::Calendar::FrenchRevolutionary</span></a> 
    également disponible <a href='https://github.com/jforget/DateTime-Calendar-FrenchRevolutionary'>sur GitHub</a>.

    En Perl également&nbsp;: <a href='http://metacpan.org/module/Date::Convert::French_Rev'><span class='tt'>Date::Convert::French_Rev</span></a>
    également disponible <a href='https://github.com/jforget/Date-Convert-French_Rev'>sur GitHub</a>.

    Existe aussi <a href='https://github.com/jhbadger/FrenchRevCal-ruby'>en Ruby</a> (mais ce n'est pas moi).

    Ou alors sous Emacs <a href='https://github.com/jforget/emacs-lisp-cal-french'>en e-LISP</a> (les fioritures sont de moi, pas la substantifique moëlle).

    Existe <a href='https://github.com/jhbadger/Thermidor-Android'>sur Android</a> (là encore, ce n'est pas moi).

    Existe aussi sur <a href='http://www.hpcalc.org/details.php?id=7309'>HP-48 et HP-50</a>
    (<a href='https://github.com/jforget/hp48-hp50-French-Revolutionary-calendar'>dépôt GitHub</a>).

    Existe enfin <a href='https://github.com/jforget/hp41-calfr'>sur HP-41</a>.
- en: |
    In Perl: <a href='http://metacpan.org/module/DateTime::Calendar::FrenchRevolutionary'><span class='tt'>DateTime::Calendar::FrenchRevolutionary</span></a> 
    also available <a href='https://github.com/jforget/DateTime-Calendar-FrenchRevolutionary'>on GitHub</a>.

    Also in Perl: <a href='http://metacpan.org/module/Date::Convert::French_Rev'><span class='tt'>Date::Convert::French_Rev</span></a>,
    available <a href='https://github.com/jforget/Date-Convert-French_Rev'>on GitHub</a>.

    Exists also <a href='https://github.com/jhbadger/FrenchRevCal-ruby'>in Ruby</a> (not from me).

    Or else with Emacs <a href='https://github.com/jforget/emacs-lisp-cal-french'>using e-LISP</a> (I did the superfluous, others did the backbone).

    Exists <a href='https://github.com/jhbadger/Thermidor-Android'>on Android</a> (not from me either).

    Exists also on <a href='http://www.hpcalc.org/details.php?id=7309'>HP-48 and HP-50</a>
    (<a href='https://github.com/jforget/hp48-hp50-French-Revolutionary-calendar'>GitHub repository</a>).

    Lastly, exists <a href='https://github.com/jforget/hp41-calfr'>on HP-41</a>.
- section: 0
  fr: Internet
  en: Internet
  level: 3
- fr: |
    Forcément, l'<a href='http://fr.wikipedia.org/wiki/Calendrier_r%C3%A9publicain'>entrée</a> de Wikipedia.

    La <a href='http://www.faqs.org/faqs/calendars/faq/part3/'>FAQ</a> de Claus Tondering (en anglais).

    L'<a href='http://h2g2.com/approved_entry/A2903636'>entrée</a> dans le site H2G2 (en anglais).

    Un <a href='http://prairial.free.fr/calendrier/calendrier.php?lien=sommairefr'>site</a> en français.
- en: |
    Of course, the <a href='http://en.wikipedia.org/wiki/French_Republican_Calendar'>Wikipedia entry</a>.

    Claus Tondering's <a href='http://www.faqs.org/faqs/calendars/faq/part3/'>FAQ</a>.

    The <a href='http://h2g2.com/approved_entry/A2903636'>entry</a> on the H2G2 website.

    A <a href='http://prairial.free.fr/calendrier/calendrier.php?lien=sommairefr'>website</a> in French.
- section: 0
  fr: Livres
  en: Books
  level: 3
- fr: |
    Quid 2001, M and D Frémy, éditions Robert Laffont.

    Agenda Républicain 197 (1988/89), éditions Syros Alternatives, ISBN 2&nbsp;86738&nbsp;262-9.

    N'importe quel livre scolaire consacré à l'Histoire de la Révolution française.

    The French Revolution, Thomas Carlyle, Oxford University Press, ISBN 0-19-281843-0, en anglais.

    Introduction à APL, S. Pommier, éditions Dunod, 1978, ISBN 2-04-010402-X.

    Apprendre et appliquer le langage APL, B. Legrand, éditions Masson, 1981, ISBN 2-225-69421-4.
- en: |
    Quid 2001, M and D Frémy, publ. Robert Laffont, in French

    Agenda Républicain 197 (1988/89), publ. Syros Alternatives, in French, ISBN 2&nbsp;86738&nbsp;262-9

    Any French schoolbook about the French Revolution

    The French Revolution, Thomas Carlyle, Oxford University Press, ISBN 0-19-281843-0, in English.

    Introduction à APL, S. Pommier, éditions Dunod, 1978, ISBN 2-04-010402-X, in French.

    Apprendre et appliquer le langage APL, B. Legrand, éditions Masson, 1981, ISBN 2-225-69421-4, in French.
